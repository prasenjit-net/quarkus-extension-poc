ext {
    revision = '1.0-SNAPSHOT'
    grouipId = 'net.prasenjit.poc.quarkus'

    boolean ci = System.getenv("CI")
    if (ci) {
        String REF = System.getenv("GITHUB_REF")
        project.logger.info("CI Environment with ref ${REF}")
        ref_values = REF.split('/')
        switch (ref_values[1]) {
            case 'heads':
                if (ref_values[2] != 'master') {
                    revision = ref_values[2] + '-SNAPSHOT'
                }
                break
            case 'tags':
                if (ref_values[2].startsWith('v') || ref_values[2].startsWith('V')) {
                    revision = ref_values[2].substring(1)
                }
                break
            case 'pull':
                revision = "PR${ref_values[2]}-SNAPSHOT"
        }
        project.logger.info('Derived Version {}', revision)
    } else {
        project.logger.info("NOT CI Environment")
    }
}

group grouipId
version revision

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'

    group grouipId
    version revision

    test {
        systemProperty('java.util.logging.manager', 'org.jboss.logmanager.LogManager')
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            html.required = false
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/prasenjit-net/quarkus-extension-poc")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from(components.java)
                pom {
                    name = "POC Quarkus Ext ${project.name}"
                }
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}
